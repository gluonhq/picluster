plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.0.8'
  id 'org.beryx.jlink' version '2.14.1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation ('eu.hansolo:tilesfx:11.10') {
        exclude group:"org.hamcrest"
        exclude group:"org.openjfx"
    }
    implementation ('org.controlsfx:controlsfx:11.0.0') {
        exclude group:"org.openjfx"
    }
    implementation 'org.kordamp.ikonli:ikonli-javafx:11.3.4'
    implementation 'org.kordamp.ikonli:ikonli-material-pack:11.3.4'
}

javafx {
    version = "12.0.2"
    modules = [ 'javafx.controls', 'javafx.web' ]
}

mainClassName = "$moduleName/com.gluonhq.iotmonitor.monitor.Main"

run {
    jvmArgs +=  [
            "--add-exports=javafx.graphics/com.sun.javafx.scene=org.controlsfx.controls",
            "--add-exports=javafx.graphics/com.sun.javafx.scene.traversal=org.controlsfx.controls"
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    targetPlatform("linux") {
        jdkHome = "$rootDir/files/filesLinux/jdk-12.0.2"
        addExtraModulePath("$rootDir/files/filesLinux/javafx-jmods-12.0.2")
    }
    targetPlatform("mac") {
        jdkHome = "$rootDir/files/filesMac/jdk-12.0.2.jdk/Contents/Home"
        addExtraModulePath("$rootDir/files/filesMac/javafx-jmods-12.0.2")
    }
    targetPlatform("win") {
        jdkHome = "$rootDir/files/filesWin/jdk-12.0.2"
        addExtraModulePath("$rootDir/files/filesWin/javafx-jmods-12.0.2")
    }
    launcher {
        jvmArgs = ["-Dprism.order=j2d",
                   "--add-exports", "javafx.graphics/com.sun.javafx.scene=org.controlsfx.controls",
                   "--add-exports", "javafx.graphics/com.sun.javafx.scene.traversal=org.controlsfx.controls"]
        name = 'iotserver'
    }
}